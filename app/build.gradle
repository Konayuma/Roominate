plugins {
    alias(libs.plugins.android.application)
}

// Load local.properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { stream ->
        localProperties.load(stream)
    }
}

android {
    namespace 'com.roominate'
    compileSdk 36

    defaultConfig {
        applicationId "com.roominate"
        minSdk 24
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Supabase build config fields loaded from local.properties
        buildConfigField "String", "SUPABASE_URL", "\"${localProperties.getProperty('SUPABASE_URL', 'https://your-project-ref.supabase.co')}\""
        buildConfigField "String", "SUPABASE_ANON_KEY", "\"${localProperties.getProperty('SUPABASE_ANON_KEY', 'your-anon-key-here')}\""
    }

    // Enable generation of BuildConfig so custom fields are created
    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation libs.appcompat
    implementation 'com.google.android.material:material:1.11.0'  // Updated for Material 3
    implementation libs.constraintlayout
    implementation libs.recyclerview
    implementation libs.viewpager2
    implementation libs.fragment
    
    // HTTP client for API calls
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    // Secure storage for OAuth tokens
    implementation 'androidx.security:security-crypto:1.1.0'
    // Image loading
    implementation 'com.squareup.picasso:picasso:2.8'
    // CardView UI
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'org.osmdroid:osmdroid-android:6.1.18'
    
    // Shimmer loading effect
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    // Pull-to-refresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}